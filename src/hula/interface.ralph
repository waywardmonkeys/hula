(define-module interface
  import: (runtime/core toolbox/dom)
  export: (<window> <resizer>))

;; window

(define-generic attach (object target))

(define-class <window> ())

(define-function initialize ((window <window>) #key label)
  (bind ((element (set! (get window "element")
                        (create-element "div" class: "window")))
         (header (set! (get window "header")
                       (create-element "div" class: "header")))
         (label-element (set! (get window "label")
                      (create-element "label")))
         (content (set! (get window "content")
                        (create-element "div" class: "content")))
         (footer  (set! (get window "footer")
                        (create-element "div" class: "footer"))))
    (append-child header label-element)
    (append-child element header)
    (append-child element content)
    (append-child element footer)
    (set! (header-label window) label)))

(define-function header-label ((window <window>))
  (js:get-property window "label" "innerText"))

(define-function (setter header-label) ((window <window>) label)
  (set! (js:get-property window "label" "innerText") label))

(define-function attach ((window <window>) element)
  (append-child element (get window "element")))

;; dragger

(define-class <dragger> ()
  handle)

(define-function initialize ((dragger <dragger>) #key handle)
  (bind ((handle (set! (get dragger "handle")
                       (or handle (create-element "div")))))
    (add-class handle "dragger")))

(define-function attach ((dragger <dragger>) (window <window>))
  (bind ((element (get window "element"))
         (handle (get dragger "handle"))
         (start-x) (start-y)
         (start-left) (start-top))
    (append-child element handle)
    (bind-methods ((move (event)
                     (bind ((x (get event "clientX"))
                            (y (get event "clientY"))
                            (left (+ start-left (- x start-x)))
                            (top (+ start-top (- y start-y)))
                            (new-left (if (< left 0) 0 left))
                            (new-top (if (< top 0) 0 top)))
                       (trigger-event window (make-event "move"))
                       (set! (element-left element) new-left)
                       (set! (element-top element) new-top))
                     (stop-propagation event)
                     (prevent-default event))
                   (up (event)
                     (remove-listener document "mouseup" up #t)
                     (remove-listener document "mousemove" move #t)
                     (set! (get element "style" "cursor")
                           "default")
                     (stop-propagation event)
                     (prevent-default event))
                   (down (event)
                     (add-listener document "mouseup" up #t)
                     (add-listener document "mousemove" move #t)
                     (set! start-x (get event "clientX"))
                     (set! start-y (get event "clientY"))
                     (set! start-left (element-left element))
                     (set! start-top (element-top element))
                     (set! (get element "style" "cursor")
                           "move")
                     (stop-propagation event)
                     (prevent-default event)))
      (add-listener handle "mousedown" down #t))))

;; closer

(define-class <closer> ()
  button)

(define-function initialize ((closer <closer>) #key button)
  (bind ((button (set! (get closer "button")
                       (or button (create-element "button")))))
    (add-class button "close")))

(define-function attach ((closer <closer>) (window <window>))
  (bind ((element (get window "element"))
         (button (get closer "button"))
         (closing? #f))
    (append-child element button)
    (bind-methods ((finished (event)
                     (when closing?
                       (delete-element element)
                       (trigger window (make-event "close"))))
                    (click (event)
                      (do (method (type)
                            (add-listener element type finished #f))
                          (make-array "webkitTransitionEnd" "transitionend"))
                      (add-class element "closed")))
      (add-listener button "click" click #f))))

;; resizer

(define-class <resizer> ()
  handle
  min-width
  max-width
  min-height
  max-height)

(define-function initialize ((resizer <resizer>) #key handle
                             min-width max-width min-height max-height)
  (bind ((handle (set! (get resizer "handle")
                       (or handle (create-element "div")))))
    (add-class handle "resizer"))
  (set! (get resizer "min-width") min-width)
  (set! (get resizer "max-width") max-width)
  (set! (get resizer "min-height") min-height)
  (set! (get resizer "max-height") max-height))

(define-function attach ((resizer <resizer>) (window <window>))
  (bind ((element (get window "element"))
         (handle (get resizer "handle"))
         (start-x) (start-y)
         (start-width) (start-height))
    (append-child element handle)
    (bind-methods ((move (event)
                     (bind ((x (get event "clientX"))
                            (y (get event "clientY"))
                            (width (+ start-width (- x start-x)))
                            (height (+ start-height (- y start-y)))
                            (new-width (if (< width (get resizer "min-width"))
                                           (get resizer "min-width")
                                           width))
                            (new-height (if (< height (get resizer "min-height"))
                                            (get resizer "min-height")
                                            height))
                            (new-width (if (> new-width (get resizer "max-width"))
                                           (get resizer "max-width")
                                           new-width))
                            (new-height (if (> new-height (get resizer "max-height"))
                                            (get resizer "max-height")
                                          new-height)))
                       (trigger-event window (make-event "resize"))
                       (set! (element-width element) new-width)
                       (set! (element-height element) new-height))
                     (stop-propagation event)
                     (prevent-default event))
                   (up (event)
                     (remove-listener document "mouseup" up #t)
                     (remove-listener document "mousemove" move #t)
                     (stop-propagation event)
                     (prevent-default event))
                   (down (event)
                     (add-listener document "mouseup" up #t)
                     (add-listener document "mousemove" move #t)
                     (set! start-x (get event "clientX"))
                     (set! start-y (get event "clientY"))
                     (set! start-width (element-width element))
                     (set! start-height (element-height element))
                     (stop-propagation event)
                     (prevent-default event)))
      (add-listener handle "mousedown" down #t))))
